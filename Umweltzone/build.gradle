import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

def versionMajor = 3
def versionMinor = 4
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def gitSha() {
    def res = "git rev-parse --short HEAD".execute([], project.rootDir).text.trim()

    def diff = "git diff".execute([], project.rootDir).text.trim()
    if (diff != null && diff.length() > 0) {
        res += "-dirty"
    }

    return res
}

def buildTime() {
    return new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            // Verbose Lint output
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode versionMajor * 100000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "BUILD_VERSION", "\"${versionName}\""
        buildConfigField "int", "BUILD_VERSION_CODE", "${versionCode}"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        archivesBaseName = "Umweltzone-$versionName"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        String sharedTestDir = "src/sharedTest/java"
        androidTest {
            java.srcDirs += sharedTestDir
        }
        test {
            java.srcDirs += sharedTestDir
        }
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            zipAlignEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard.cfg"
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        pickFirst "META-INF/LICENSE"
        pickFirst "META-INF/LICENSE.txt"
        pickFirst "META-INF/NOTICE"
        pickFirst "META-INF/NOTICE.txt"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

tasks.withType(Test) {
    testLogging {
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_ERROR,
                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showCauses true
        showExceptions true
        showStackTraces true
    }
}

configurations {
    // http://qathread.blogspot.de/2015/02/discovering-espresso-for-android.html
    testImplementation.exclude group: "com.android.support", module: "support-annotations"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "com.android.support:cardview-v7:$androidSupportVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support:preference-v7:$androidSupportVersion"
    implementation "com.google.android.gms:play-services-base:$playServicesVersion"
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.github.ligi.snackengage:snackengage-playrate:$snackengagePlayrateVersion"
    implementation "de.cketti.library.changelog:ckchangelog:$ckChangelogVersion"
    implementation "info.metadude.android:typed-preferences:$typedPreferencesVersion"
    implementation "org.parceler:parceler-api:$parcelerVersion"
    kapt "org.parceler:parceler:$parcelerVersion"
    implementation files("libs/libGoogleAnalyticsServices.jar")
    implementation files("libs/TraceDroid-1.1.jar")

    // Integration tests
    androidTestImplementation "com.android.support:support-annotations:$androidSupportVersion"
    androidTestImplementation("com.squareup.assertj:assertj-android:$assertjAndroidVersion") {
        exclude group: "com.android.support", module: "support-annotations"
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espressoVersion") {
        exclude group: "com.android.support", module: "support-annotations"
    }
    androidTestImplementation("com.android.support.test:rules:$rulesVersion") {
        exclude group: "com.android.support", module: "support-annotations"
    }

    // Unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "com.squareup.assertj:assertj-android:$assertjAndroidVersion"
}

Properties props = new Properties()
File gradlePropertiesFile = file("gradle.properties")
if (gradlePropertiesFile.exists()) {
    props.load(new FileInputStream(gradlePropertiesFile))
}

if (project.hasProperty("signing.release.keystoreFilePath")) {
    android.signingConfigs.release.storeFile = file(props["signing.release.keystoreFilePath"])
}
if (project.hasProperty("signing.release.keystorePassword")) {
    android.signingConfigs.release.storePassword = props["signing.release.keystorePassword"]
}
if (project.hasProperty("signing.release.keyAlias")) {
    android.signingConfigs.release.keyAlias = props["signing.release.keyAlias"]
}
if (project.hasProperty("signing.release.keyPassword")) {
    android.signingConfigs.release.keyPassword = props["signing.release.keyPassword"]
}
