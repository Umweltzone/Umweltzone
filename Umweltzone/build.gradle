apply plugin: 'com.android.application'
apply plugin: 'org.standardout.versioneye'
apply plugin: 'com.github.ben-manes.versions'

versioneye {
    exclude 'androidJacocoAgent', 'androidJacocoAnt'
}

repositories {
    mavenCentral()
}

def versionMajor = 2
def versionMinor = 7
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def gitSha() {
    def res = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

    def diff = 'git diff'.execute([], project.rootDir).text.trim()
    if (diff != null && diff.length() > 0) {
        res += "-dirty"
    }

    return res
}

def buildTime() {
    return new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            // Verbose Lint output
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 22
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "BUILD_VERSION", "\"${versionName}\""
        buildConfigField "int", "BUILD_VERSION_CODE", "${versionCode}"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        archivesBaseName = "Umweltzone-$versionName"
    }

    signingConfigs {
        release
    }

    aaptOptions {
        useNewCruncher true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            zipAlignEnabled true
        }
        release {
            minifyEnabled true
            proguardFile 'proguard.cfg'
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile "com.android.support:design:22.2.1"
    compile "com.google.android.gms:play-services:3.2.65"
    compile "com.fasterxml.jackson.core:jackson-databind:2.7.1-1"
    compile "de.cketti.library.changelog:ckchangelog:1.2.2"
    compile "info.metadude.android:typed-preferences:2.1.0"
    compile files("libs/libGoogleAnalyticsServices.jar")
    compile files("libs/TraceDroid-1.1.jar")

    // Unit tests
    testCompile "junit:junit:4.12"
    testCompile("com.squareup.assertj:assertj-android:1.1.1") {
        exclude module: "support-annotations"
    }
}

Properties props = new Properties()
File gradlePropertiesFile = file("gradle.properties")
if (gradlePropertiesFile.exists()) {
    props.load(new FileInputStream(gradlePropertiesFile))
}

if (project.hasProperty('signing.release.keystoreFilePath')) {
    android.signingConfigs.release.storeFile = file(props['signing.release.keystoreFilePath'])
}
if (project.hasProperty('signing.release.keystorePassword')) {
    android.signingConfigs.release.storePassword = props['signing.release.keystorePassword']
}
if (project.hasProperty('signing.release.keyAlias')) {
    android.signingConfigs.release.keyAlias = props['signing.release.keyAlias']
}
if (project.hasProperty('signing.release.keyPassword')) {
    android.signingConfigs.release.keyPassword = props['signing.release.keyPassword']
}
