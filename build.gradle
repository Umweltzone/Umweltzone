// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        // Plugins
        androidGradlePluginVersion = "3.1.0"
        gradleVersioneyePlugin = "1.5.0"
        gradleVersionsPluginVersion = "0.20.0"
        proguardGradlePluginVersion = "6.0.3"

        // Libraries
        androidSupportVersion = "24.2.1"
        assertjAndroidVersion = "1.2.0"
        ckChangelogVersion = "1.2.2"
        espressoVersion = "3.0.2"
        jacksonVersion = "2.8.10"
        junitVersion = "4.12"
        parcelerVersion = "1.1.11"
        playServicesVersion = "10.0.1"
        rulesVersion = "1.0.2"
        snackengagePlayrateVersion = "0.15"
        typedPreferencesVersion = "2.1.0"
    }

    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath "org.standardout:gradle-versioneye-plugin:$gradleVersioneyePlugin"
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsPluginVersion"
        classpath "net.sf.proguard:proguard-gradle:$proguardGradlePluginVersion"
    }
}

apply plugin: "org.standardout.versioneye"
apply plugin: "com.github.ben-manes.versions"

versioneye {
    includeSubProjects = true
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}
