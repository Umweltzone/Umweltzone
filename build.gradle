// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        // Plugins
        androidGradlePluginVersion = "3.3.2"
        gradleVersionsPluginVersion = "0.21.0"
        proguardGradlePluginVersion = "6.0.3"

        // Libraries
        androidSupportVersion = "25.4.0"
        assertjAndroidVersion = "1.2.0"
        ckChangelogVersion = "1.2.2"
        constraintLayoutVersion = "1.1.3"
        espressoVersion = "2.2.2"
        jacksonVersion = "2.8.10"
        junitVersion = "4.12"
        kotlinVersion = "1.3.21"
        parcelerVersion = "1.1.12"
        playServicesVersion = "10.0.1"
        snackengagePlayrateVersion = "0.15"
        typedPreferencesVersion = "2.1.0"
    }

    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsPluginVersion"
        classpath "net.sf.proguard:proguard-gradle:$proguardGradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: "com.github.ben-manes.versions"

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}
